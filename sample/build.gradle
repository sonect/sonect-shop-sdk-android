apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.appdistribution'

def getVersionCode = { ->
    def DEFAULT_CODE = 1
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : DEFAULT_CODE
    println "VersionCode is set to $code"
    return code
}

def sonectSdkVersion = "1.6.3"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ch.sonect.sdk.shop.integrationapp"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName "1.0+" + getVersionCode()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true
        missingDimensionStrategy 'sdkDependency', 'external'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        missingDimensionStrategy 'sdkDependency', 'external'
        missingDimensionStrategy 'okhttpVersion', 'v4'
    }

    signingConfigs {
        // Custom debug store to be sure that all tools e.g. have the same
        // hash of the key to work properly.
        customDebug {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.customDebug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                groups="internal"
                releaseNotes="Sonect SDK version inside: $sonectSdkVersion"
            }
        }

        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.customDebug
        }
    }

    flavorDimensions("sdkDependency")
    productFlavors {
        internal {
            dimension = "sdkDependency"
        }
        external {
            dimension = "sdkDependency"
        }
    }
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.firebase:firebase-appdistribution-gradle:2.0.1'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.72"

    implementation 'com.google.dagger:dagger:2.27'
    kapt 'com.google.dagger:dagger-compiler:2.27'
    api 'com.google.dagger:dagger-android:2.27'
    api 'com.google.dagger:dagger-android-support:2.27' // if you use the support libraries
    kapt 'com.google.dagger:dagger-android-processor:2.27'


    externalImplementation ("com.github.sonect:android-shop-sdk:$sonectSdkVersion") {
        exclude module: "okhttp4"
    }

    // For Sonect internal usage and direct dependency to source code
    internalImplementation project(path: ':sdk-shop')
    internalImplementation project(path: ':sdk-common')
    internalImplementation project(path: ':sdk-data')
    internalImplementation project(path: ':sdk-domain')
    internalImplementation project(path: ':okhttp')

    // Idenfy
    implementation 'idenfySdk:com.idenfy.idenfySdk:2.6.0'
    implementation 'idenfySdk:com.idenfy.idenfySdk.idenfyliveness:2.6.0'

    externalImplementation "com.squareup.okhttp3:okhttp:3.14.9"
    externalImplementation "com.squareup.okhttp3:logging-interceptor:3.14.9"
}

apply plugin: 'com.google.gms.google-services'
